set term pdf size 4,5
set output 'ExampleIncidentRates.pdf'


TrackWidth = 1;
Speed = 10000;
AKendall = 1e6;
nKendallStartZones = 6/1e6
AStartZone = 15*20
fTrigger = 0.5
AAvalanche = 0.01*1e6 


rSP(n) = TrackWidth * Speed /AKendall * n * nKendallStartZones * AStartZone *fTrigger

rIP(n) = rSP(n) * AAvalanche * n

set style fill transparent pattern 4 bo
set style fill transparent pattern 2 bo

set key top left noauto

set multiplot layout 2,1

set xlabel 'Party Density (parties/km^2)'

set margins 11,12,-1,-1
unset ylabel

set ytics nomirror
set y2tics ("100" 0.01, "50" 0.02, "30" 0.0333,"20" 0.05, "15" 0.067, "10" 0.1) 

pl [0:200][0:0.006]\
	(rIP(x/1e6) + rSP(x/1e6))*1e6 lt  3      lw 2 tit 'Total',\
	rIP(x/1e6)*1e6               lt -1 dt 2 lw 2 tit 'Inter-Party Rate',\
	rSP(x/1e6)*1e6               lt  2 dt 5 lw 2 tit 'Single-Party Rate'
	

set logsc
set format y "10^{%T}"
set ytics mirror
set logsc y2
set y2tics ("100,000" 0.00001,"10,000" 0.0001, "1,000" 0.001, "100" 0.01, "10" 0.1, "1" 1) 

pl [1:1000][1e-5:0.1][:][1e-5:0.1]\
	(rIP(x/1e6) + rSP(x/1e6))*1e6 lt  3      lw 2 tit 'Total',\
	rIP(x/1e6)*1e6               lt -1 dt 2 lw 2 tit 'Inter-Party Rate',\
	rSP(x/1e6)*1e6               lt  2 dt 5 lw 2 tit 'Single-Party Rate'
	

set ylabel "Incidents /day/km^2\n{/*0.7 Assuming: w = 1m, s = 10km/d, n_{start-zones} = 6/km^2, A_{start-zone} = 300m^2, f_{trigger} = 0.5, A_{avalanche} = 0.01km^2}"
set y2label '      Days between incidents / km^2 (same assumptions)'

#####(Assuming: L=0.1,A_{avalanche} = 2000 m^2, A_{start} = 290 m^2)\nIncidents /day/km^2"#####

set lmargin -1
set rmargin -1
set origin 0,0
set size 1,1
set border 0
unset tics
unset xlabel

plot [][0:1] -1

unset multiplot

