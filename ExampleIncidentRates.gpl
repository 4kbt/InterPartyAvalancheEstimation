set term pdf size 4,5
set output 'ExampleIncidentRates.pdf'


TrackWidth = 1;
Speed = 10000;
AKendall = 1e6;
nKendallStartZones = 6/AKendall
AStartZone = 20*20
fTrigger = 0.002
AAvalanche = 0.2*1e6 


rSP(n) = TrackWidth * Speed * nKendallStartZones * AStartZone *fTrigger * n

rIP(n) = rSP(n) * AAvalanche * n

set key top left noauto

set multiplot layout 2,1

set xlabel 'Party Density (parties/km^2)'

set margins 13,12,-1,-1
unset ylabel

set ytics nomirror
set y2tics ("10" 0.1, "2" 0.5, "1" 1)

pl [0:10][0:1.5]\
	(rIP(x/1e6) + rSP(x/1e6))*1e6 lt  3      lw 2 tit 'Total',\
	rIP(x/1e6)*1e6               lt -1 dt 2 lw 2 tit 'Inter-Party Rate',\
	rSP(x/1e6)*1e6               lt  2 dt 5 lw 2 tit 'Single-Party Rate'
	
set logsc
set format y "10^{%T}"
set ytics mirror
set logsc y2
set y2tics ("1,000" 0.001, "100" 0.01, "10" 0.1, "1" 1, "0.1" 10, "0.01" 100) 

pl [0.1:100][1e-3:100][:][1e-3:100]\
	(rIP(x/1e6) + rSP(x/1e6))*1e6 lt  3      lw 2 tit 'Total',\
	rIP(x/1e6)*1e6               lt -1 dt 2 lw 2 tit 'Inter-Party Rate',\
	rSP(x/1e6)*1e6               lt  2 dt 5 lw 2 tit 'Single-Party Rate'
	

set ylabel "Involvements /day/km^2\n{/*0.7 Assuming: w = 1m, s = 10km/d, n_{start-zones} = 6/km^2, A_{start-zone} = 400m^2, f_{trigger} = 0.002/m^2, A_{avalanche} = 0.2km^2}"
set y2label '      Days between incidents / km^2 (same assumptions)'

#####(Assuming: L=0.1,A_{avalanche} = 2000 m^2, A_{start} = 290 m^2)\nIncidents /day/km^2"#####

set lmargin -1
set rmargin -1
set origin 0,0
set size 1,1
set border 0
unset tics
unset xlabel
unset logsc

plot [][0:1] -1

unset multiplot

